apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'signing'

group = 'org.asteriskjava'
version = '2.0.0.CI-SNAPSHOT'

description = """Asterisk-Java"""

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
        
     maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'log4j', name: 'log4j', version:'1.2.17'
    compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.9'
    testCompile group: 'org.easymock', name: 'easymock', version:'3.3'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version:'1.1.2'
}

test {
    testLogging {
        events 'started', 'passed'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

def ossrhUsername = hasProperty('ossrhUsername') ? ossrhUsername : ''
def ossrhPassword = hasProperty('ossrhPassword') ? ossrhPassword : ''

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Asterisk-Java'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'The free Java library for Asterisk PBX integration.'
                url 'https://github.com/asterisk-java/asterisk-java'

                scm {
                    connection 'scm:git:https://github.com/asterisk-java/asterisk-java.git'
                    developerConnection 'scm:git:https://github.com/asterisk-java/asterisk-java.git'
                    url 'https://github.com/asterisk-java/asterisk-java'
                }

                licenses {
                    license {
                        name 'Apache 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.html'
                    }
                }

                mailingLists {
                    mailingList {
                        name 'Asterisk-Java User List'
                        subscribe "http://lists.sourceforge.net/mailman/listinfo/asterisk-java-users"
                        unsubscribe "http://lists.sourceforge.net/mailman/listinfo/asterisk-java-users"
                        archive "http://sourceforge.net/mailarchive/forum.php?forum=asterisk-java-users"
                    }
                    mailingList {
                        name "Asterisk-Java Developer List"
                        subscribe "http://lists.sourceforge.net/mailman/listinfo/asterisk-java-devel"
                        unsubscribe "http://lists.sourceforge.net/mailman/listinfo/asterisk-java-devel"
                        archive "http://sourceforge.net/mailarchive/forum.php?forum=asterisk-java-devel"
                    }
                }

                developers {
                    developer {
                        id 'srt'
                        name 'Stefan Reuter'
                        email 'stefan.reuter at reucon.com'
                        timezone '+1'
                    }
                    developer {
                        name 'Pierre-Yves Roger'
                        id 'partoutatis'
                        email 'partoutatis at users.sourceforge.net'
                        timezone '+1'
                    }
                    developer {
                        name 'John Hood'
                        id 'squinky86'
                        email 'john at asteriasgi.com'
                        organization 'Asteria Solutions Group, Inc.'
                        timezone '-6'
                    }
                    developer {
                        name 'Martin B. Smith'
                        id 'martins'
                        email 'martins at bebr.ufl.edu'
                        organization 'Bureau of Economic and Business Research, University of Florida'
                        timezone '-5'
                    }
                    developer {
                        name 'S. Brett Sutton'
                        id 'bsutton'
                        email 'bsutton at noojee.com.au'
                        organization 'Noojee Telephony Solutions Pty Ltd'
                        timezone '+10'
                    }
                    developer {
                        name 'Octavio Luna'
                        id 'itaqua'
                        email 'octavio.luna at gmail.com'
                        organization 'N4Utilus Take your Business to the Cloud'
                        timezone '-6'
                    }
                    developer {
                        name 'Zoumana TRAORE'
                        id 'zoumhussein'
                        email 'github at zoumanatraore dot fr'
                        timezone '+1'
                    }
                }
            }
        }
    }
}